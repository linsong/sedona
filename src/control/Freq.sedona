//
// Copyright (c) 2007-2011 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jan 2007  Andy Saunders  Creation
//   27 Apr 2007  Brian Frank    Port from Java to Sedona
//   22 Apr 2009  Elizabeth McKenney   Updated
//   04 Aug 2011  Clif Turman    Add comments.  Add units to pps and ppm.
//

**
** Freq object calculates a pulse input frequency
**
** out is in pulses per second or pulses per minute
** Note: frequency is limited by execute scan frequency 
**
@niagaraIcon="module://icons/x16/control/control.png"
class Freq
  extends Component
{
  ** frequency in pulses per second
  @unit=Units.per_second
  @readonly property float pps
  
  ** frequency in pulse per minute
  @unit=Units.per_minute
  @readonly property float ppm
  property bool in

  **
  ** Callback when component is first started in an app.
  **
  virtual override void start()
  {
    lastIn = in 
  }


  **
  ** Execute is called once every scan.
  **
  virtual override void execute()
  {
    long now = Sys.ticks()

    float period = (float)(now - lastEdge)

    if (period != 0.0f)
    {
      pps = float1sec / period
      ppm = pps * 60.0
    }

    if (in && !lastIn)  // rising edge detected
      lastEdge = now

    lastIn = in
  }

  private bool lastIn
  private long lastEdge = 0L

  // const float version of 1sec long
  private define float float1sec = (float)1sec

}
