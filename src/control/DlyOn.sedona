//
// Copyright (c) 2007 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jan 07  Andy Saunders  Creation
//   27 Apr 07  Brian Frank    Port from Java to Sedona
//   22 Apr 09  Elizabeth McKenney Simplified logic
//

**
** DlyOn Delay On object
**
**    Delays the off to on transition by a time delay
**
@niagaraIcon="module://icons/x16/control/control.png"
class DlyOn
  extends Component
{

  @readonly property bool out
  property bool in
  @config @summary property float delayTime  // specified hold time in sec
  @readonly property int hold                // remaining hold time in ms

  **
  ** Callback when component is first started in an app.
  **
  virtual override void start()
  {
    // Must initialize lastIn before first call to execute()
    lastIn = in    
  }


  **
  ** Execute is called once every scan.
  **
  virtual override void execute()
  {
    long now = Sys.ticks()

    // On positive edge transition, start delay
    if (in && !lastIn)
    {
      delayEnd = now + (long)delayTime * 1sec
    }
        
    // If input is false, cancel any delay and set output false
    if (!in)
    {
      delayEnd = 0L
      out = false
    }
    // If input is true, output is false if delay not expired;
    // o/w output is true and delay is reset
    else
    {
      out = delayEnd < now

      // If out is true, delay has expired
      if (out) delayEnd = 0L
    }

    // Update hold property to reflect delay time remaining
    if (delayEnd == 0L)
      hold = 0
    else
      hold = (int)(delayEnd - now)

    lastIn = in
  }

  private long delayEnd = 0L
  private bool lastIn
}
