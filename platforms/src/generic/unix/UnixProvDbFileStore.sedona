//
// Copyright (c) 2009 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   14 Nov 11  Elizabeth McKenney  Creation
//

**
** UnixProvDbFileStore converts filenames for the Manifest (or Kit) Database.
**                 
final class UnixProvDbFileStore 
  extends FileStore
{                      
  **
  ** Implementation for 'File.size'.  
  ** Must work without opening the file.
  **
  override virtual int size(File f) 
  {
    return doSize( substPath(f.name) )
  }

  **
  ** Implementation for 'File.open'.
  **
  override virtual bool open(File f, Str mode) 
  { 
    f.fp = doOpen( substPath(f.name), mode )
    return f.fp != null
  }


////////////////////////////////////////////////////////////////
// accept
////////////////////////////////////////////////////////////////

  **
  ** Parse the given filename, and return true if this is the appropriate
  ** FileStore instance to handle it.  
  **
  override virtual bool accept(Str filename)
  {
    return filename.startsWith(prefix) 
  }



//////////////////////////////////////////////////////////////////////////
// Natives
//////////////////////////////////////////////////////////////////////////



//////////////////////////////////////////////////////////////////////////
// Utilities
//////////////////////////////////////////////////////////////////////////

  **
  ** Strip prefix from filename, and substitute full path in its place.
  ** 
  internal Str substPath(Str fname)
  {
    Str nameStr = fname.suffix( prefix.length() )

    pathBuf.clear()

    // Write path
    fullpath.writeBytes(pathStr.toBytes(), 0, pathStr.length())

    // Write kit name (nameStr up to dash; NOP if no dash)
    int dash = nameStr.index('-')
    if (dash>=0)
    {
      fullpath.writeBytes(nameStr.toBytes(), 0, dash)
      fullpath.write('/')
    }

    // Write filename
    fullpath.writeBytes(nameStr.toBytes(), 0, nameStr.length())
    fullpath.write(0)

    return pathBuf.toStr()
  }


//////////////////////////////////////////////////////////////////////////
// Constructor
//////////////////////////////////////////////////////////////////////////

  **
  ** Constructor sets instance fields and inserts self into FileStore list.
  ** Note: assume args are (const) string literals, no need to copy.
  **
  UnixProvDbFileStore(Str pre, Str path)
  {
    this.prefix  = pre
    this.pathStr = path

    FileStore.insert(this)
  }



////////////////////////////////////////////////////////////////
// Fields
////////////////////////////////////////////////////////////////

  Str prefix
  Str pathStr

  define int maxPathLen = 256


  // Cache buffer for full path - shared by all UnixProvDbFileStore instances
  //  (assumes full path only needed when calling doSize or doOpen, not after file is open)
  static inline Buf(maxPathLen)        pathBuf
  static inline BufOutStream(pathBuf)  fullpath
  

  //
  // Static singleton instance(s) defined here;
  //   to support a new scheme, add singleton instance for it 
  //
  
  static inline UnixProvDbFileStore("m:", "manifests/") mfStore

  //static inline UnixProvDbFileStore("k:", "kits/") kitStore    // uncomment to support kits also

}

